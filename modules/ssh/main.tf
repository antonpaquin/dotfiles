variable "home" {
    type = string
    description = "home directory location"
}

locals {
    _machines = {
        cirno: "192.168.0.100"
        reimu: "192.168.0.103"
        "reimu-00": "192.168.0.105"
        hakurei: "192.168.0.108"
    }
    _kcirno_ps50 = "10.0.3.20"

    _base_props = {
        User: "user"
        Port: 22
        IdentityFile: "${var.home}/.ssh/seed.priv"
    }

    _kattegat_internal = [
        for k, v in local._machines:
        {
            host: k,
            props: merge(local._base_props, {
                HostName: v
            })
        }
    ]

    _kattegat_external = [
        for entry in local._kattegat_internal:
        {
            host: join("", ["k", entry.host])
            props: merge(
                entry.props,
                entry.host == "cirno" ? {HostName: local._kcirno_ps50} : {ProxyJump: "kcirno"}
            ),
        }
    ]

    _other = [
        {
            host: "m4",
            props: {
                HostName: "10.0.0.14"
                User: "admin",
                Port: 22,
                IdentityFile: "${var.home}/.ssh/seed.priv"
            }
        },
        {
            host: "ubiquiti",
            props: {
                HostName: "10.0.0.3"
                User: "admin",
                Port: 22,
            }
        },
        {
            host: "util",
            props: {
                HostName: "util.antonpaqu.in"
                User: "ec2-user"
                Port: 22
                IdentityFile: "${var.home}/.ssh/opus-051020.pem"
            }
        },
        {
            host: "yomi",
            props: {
                HostName: "10.10.10.1"
                User: "anton"
                Port: 22
                IdentityFile: "${var.home}/.ssh/seed.priv"
                ProxyJump: "chiyo"
            }
        },
        {
            host: "chiyo",
            props: {
                HostName: "104.10.252.237",
                User: "anton",
                Port: 22,
                IdentityFile: "${var.home}/.ssh/seed.priv",
            }
        },
        {
            host: "sakaki",
            props: {
                HostName: "10.10.10.3",
                User: "anton",
                Port: 22,
                IdentityFile: "${var.home}/.ssh/seed.priv",
                ProxyJump: "chiyo"
            }
        },
        {
            host: "osaka",
            props: {
                HostName: "10.10.10.4",
                User: "anton",
                Port: 22,
                IdentityFile: "${var.home}/.ssh/seed.priv",
                ProxyJump: "chiyo"
            }
        },
        {
            host: "pi",
            props: {
                HostName: "192.168.4.85",
                User: "user",
                Port: 22,
                IdentityFile: "${var.home}/.ssh/seed.priv",
            }
        }
    ]

    entries = concat(local._kattegat_internal, local._kattegat_external, local._other)
}

resource "local_file" "sshconf" {
    filename = "${var.home}/.ssh/config"
    content = join("\n\n", concat(
        [
            join("\n", [
                "# Autogenerated by rice terraform (~/code/dotfiles)",
                "# Changes may be overwritten!"
            ])
        ],
        [
            for entry in local.entries:
            join("\n", concat(
                ["Host ${entry.host}"],
                [
                    for k, v in entry.props:
                    "    ${k} ${v}"
                ]
            ))
        ],
    ))
    file_permission = "0640"
}
